include "hexld3_line2_symbols.asm"

	org 0x4233

LENS:	db 0x5F, 0x55, 0x55, 0xA5, 0x55, 0x55, 0x55, 0xA5
        db 0xAF, 0x55, 0x55, 0xA5, 0xA5, 0x55, 0x55, 0xA5
        db 0xAF, 0xF5, 0x55, 0xA5, 0xA5, 0xF5, 0x55, 0xA5
        db 0xAF, 0xF5, 0x99, 0xE5, 0xA5, 0xF5, 0x55, 0xA5
        db 0x55, 0x55, 0x55, 0x95, 0x55, 0x55, 0x55, 0x95
        db 0x55, 0x55, 0x55, 0x95, 0x55, 0x55, 0x55, 0x95
        db 0x55, 0x55, 0x55, 0x95, 0x55, 0x55, 0x55, 0x95
        db 0x99, 0x99, 0x99, 0x59, 0x55, 0x55, 0x55, 0x95
        db 0x55, 0x55, 0x55, 0x95, 0x55, 0x55, 0x55, 0x95
        db 0x55, 0x55, 0x55, 0x95, 0x55, 0x55, 0x55, 0x95
        db 0x55, 0x55, 0x55, 0x95, 0x55, 0x55, 0x55, 0x95
        db 0x55, 0x55, 0x55, 0x95, 0x55, 0x55, 0x55, 0x95
        db 0x55, 0xFF, 0xF5, 0xA5, 0x55, 0xFE, 0xFF, 0xA5
        db 0x55, 0xFA, 0xF5, 0xA5, 0x55, 0xFA, 0xF5, 0xA5
        db 0x55, 0xF5, 0xF5, 0xA5, 0x55, 0xF5, 0xFA, 0xA5
        db 0x55, 0xF5, 0xF5, 0xA5, 0x55, 0xF5, 0xF5, 0xA5

LLIST:	ld hl, (ADDRESS)
	ld b, 0x14		; Counter
	
START:	dec hl
NEXT:	inc hl

	ld (ADDRESS),hl

	ld a, 0x75		; Print Newline
	inc a
	call APRINT

	ld a,h			; Print address
	call HPRINT

	ld a,l
	call HPRINT

	xor a			; Print space
	call APRINT

	push bc			; Save row counter
	
	ld c,0x00

BYTE:	ld a,(hl)		; Check if DD/FD prefix instruction
	cp 0xDD
	jr z, DDFD
	cp 0xFD
	jr nz, NORM

DDFD:	call HPRINT
	inc hl
	inc c
	jr BYTE

NORM:	cp 0xED
	jr nz, NOTED
	call HPRINT
	inc hl
	ld a,(hl)
	and 0xC7
	cp 0x43
	jr nz,ONE
	ld b,0x03
	jr THREE
ONE:	ld b,0x01

THREE:	ld a,(hl)
	call HPRINT
	inc hl
	
	djnz THREE

	dec hl
	jr NEXTINST

NOTED:	push hl
	sra a
	push af

	and 0x7f
	add a, LENS & 0xFF
	ld l,a
	ld h, LENS >> 8

	pop af

	ld a,(hl)
	jr c, DIG2

	rra
	rra
	rra
	rra

DIG2:	dec c
	jr nz, OK

	rra
	rra

OK:	and 0x03
	ld b,a

	pop hl
	dec hl
NXBYT:	inc hl
	ld a,(hl)
	call HPRINT
	djnz NXBYT

NEXTINST:
	pop bc
	djnz NEXT

	jp DONE
