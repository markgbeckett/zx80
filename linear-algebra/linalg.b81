  10 REM LINEAR-SYSTEM SOLVER
  20 GOSUB 600
  30 CLS 
  40 GOSUB 400
  50 GOSUB 200
  60 GOSUB 300
  70 PRINT 
  80 GOSUB 500
  90 STOP 
 200 REM REDUCTION
 210 FOR J=1 TO N-1
 220 FOR I=J+1 TO N
 230 LET M=A(I,J)/A(J,J)
 240 FOR K=J TO N
 250 LET A(I,K)=A(I,K)-M*A(J,K)
 260 NEXT K
 265 LET B(I)=B(I)-M*B(J)
 270 NEXT I
 280 NEXT J
 290 RETURN 
 300 REM BACK SUBSTITUTION
 310 FOR I=N TO 1 STEP -1
 320 LET C(I)=B(I)
 330 FOR J=I+1 TO N
 340 LET C(I)=C(I)-A(I,J)*C(J)
 350 NEXT J
 360 LET C(I)=C(I)/A(I,I)
 370 NEXT I
 380 RETURN 
 400 REM PRINT SYSTEM
 410 FOR I=1 TO N
 420 FOR J=1 TO N
 430 PRINT A(I,J); " ";
 440 NEXT J
 450 PRINT "  ";B(I)
 460 NEXT I
 470 RETURN 
 500 REM PRINT SOLN
 510 FOR I=1 TO N
 520 PRINT "C(";I;")=";C(I)
 530 NEXT I
 540 RETURN 
 600 REM ENTER SYSTEM
 610 PRINT "ENTER N"
 620 INPUT N
 630 PRINT "N = "; N
 640 DIM A(N,N)
 650 DIM B(N)
 660 DIM C(N)
 670 FOR I=1 TO N
 680 FOR J=1 TO N
 690 PRINT "ENTER A(";I;",";J;")";
 700 INPUT A(I,J)
 710 PRINT A(I,J)
 720 NEXT J
 730 PRINT "ENTER B(";I;")";
 740 INPUT B(I)
 750 PRINT B(I)
 760 NEXT I
 770 CLS 
 780 GOSUB 400
 790 PRINT "OK?"
 800 INPUT I$
 810 IF I$="Y" THEN RETURN 
 820 GOTO 670
