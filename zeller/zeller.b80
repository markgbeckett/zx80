  10   REM ZELLERS CONGRUENCE
  20 CLS 
  30 GO SUB 100
  40 GO SUB 400
  50 GO SUB 500
  60 PRINT "AGAIN? (Y/N)"
  70 INPUT I$
  80 IF I$="Y" THEN GO TO 20
  90 STOP 
 100   REM ENTER DATE
 110 PRINT "ENTER DATE (DD/MM/YYYY)"
 120 INPUT D$
 130 LET Q=10*(CODE(D$)-28)
 140 LET D$=TL$(D$)
 150 LET Q=Q+(CODE(D$)-28)
 160 LET D$=TL$(TL$(D$))
 170 LET M=10*(CODE(D$)-28)
 180 LET D$=TL$(D$)
 190 LET M=M+(CODE(D$)-28)
 200 LET D$=TL$(TL$(D$))
 210 LET Y=1000*(CODE(D$)-28)
 220 LET D$=TL$(D$)
 230 LET Y=Y+100*(CODE(D$)-28)
 240 LET D$=TL$(D$)
 250 LET Y=Y+10*(CODE(D$)-28)
 260 LET D$=TL$(D$)
 270 LET Y=Y+(CODE(D$)-28)
 280 PRINT "DAY = ";Q
 290 PRINT "MONTH = ";M
 300 PRINT "YEAR = ";Y
 310 PRINT 
 320 PRINT "DATE OK? (Y/N)"
 330 INPUT I$
 340 IF I$="Y" THEN RETURN 
 350 CLS 
 360 GO TO 110
 400 REM COMPUTE DAY
 410 IF M<3 THEN GO SUB 900
 420 LET J=Y/100
 430 LET K=Y-100*J
 440 LET D=Q+13*(M+1)/5+5*K/4+21*J/4
 450 LET DQ=D/7
 460 LET D=D-7*DQ
 470 RETURN 
 500 REM PRINT DAY STRING
 510 PRINT "DAY IS ";
 520 IF D=0 THEN PRINT "SATURDAY"
 530 IF D=1 THEN PRINT "SUNDAY"
 540 IF D=2 THEN PRINT "MONDAY"
 550 IF D=3 THEN PRINT "TUEDAY"
 560 IF D=4 THEN PRINT "WEDNESDAY"
 570 IF D=5 THEN PRINT "THURSDAY"
 580 IF D=6 THEN PRINT "FRIDAY"
 590 PRINT 
 600 RETURN 
 900   REM STEP BACK ONE YEAR
 910 LET Y=Y-1
 920 LET M=M+12
 930 RETURN 
